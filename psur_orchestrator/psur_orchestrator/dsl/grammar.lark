// PSUR DSL Grammar
// Supports SOURCE, OBLIGATION, and CONSTRAINT declarations

start: declaration*

declaration: source_decl
           | obligation_decl
           | constraint_decl
           | import_decl

import_decl: "IMPORT" STRING

source_decl: "SOURCE" STRING "{" source_field* "}"

source_field: "jurisdiction" ":" JURISDICTION -> sf_jurisdiction
            | "instrument" ":" STRING -> sf_instrument
            | "effective_date" ":" DATE -> sf_effective_date
            | "title" ":" STRING -> sf_title

obligation_decl: "OBLIGATION" STRING "{" obligation_field* "}"

obligation_field: "title" ":" STRING -> of_title
                | "jurisdiction" ":" JURISDICTION -> of_jurisdiction
                | "mandatory" ":" BOOLEAN -> of_mandatory
                | "required_evidence_types" ":" string_list -> of_required_evidence_types
                | "allowed_transformations" ":" string_list -> of_allowed_transformations
                | "forbidden_transformations" ":" string_list -> of_forbidden_transformations
                | "required_time_scope" ":" STRING -> of_required_time_scope
                | "allowed_output_types" ":" string_list -> of_allowed_output_types
                | "sources" ":" string_list -> of_sources
                | "allow_absence_statement" ":" BOOLEAN -> of_allow_absence_statement

constraint_decl: "CONSTRAINT" STRING "{" constraint_field* "}"

constraint_field: "severity" ":" SEVERITY -> cf_severity
                | "trigger" ":" STRING -> cf_trigger
                | "if" ":" STRING -> cf_if
                | "then" ":" STRING -> cf_then
                | "sources" ":" string_list -> cf_sources
                | "jurisdiction" ":" JURISDICTION -> cf_jurisdiction

string_list: "[" [STRING ("," STRING)*] "]"

JURISDICTION: "EU" | "UK" | "FDA" | "HEALTH_CANADA" | "TGA"
SEVERITY: "BLOCK" | "WARN"
BOOLEAN: "true" | "false"
DATE: /\d{4}-\d{2}-\d{2}/
STRING: /"[^"]*"/

COMMENT: "//" /[^\n]/*
%ignore COMMENT
%ignore /\s+/
